### Comparison between HBase and Hive

1. **Hive**
    - **Data Warehouse**: Essentially, Hive creates a bi-directional relationship between files stored in HDFS and MySQL to facilitate the use of HQL for management and querying.
    - **Used for Data Analysis and Cleaning**: Suitable for offline data analysis and cleaning, but it has higher latency.
    - **Based on HDFS and MapReduce**: Data stored in Hive remains on DataNodes, and HQL statements are ultimately converted into MapReduce jobs for execution.

2. **HBase**
    - **Database**: A non-relational database that stores data in column families.
    - **Used for Storing Structured and Unstructured Data**: Suitable for single table non-relational data storage, but not for complex queries like JOINs.
    - **Based on HDFS**: Data is stored persistently as HFiles in DataNodes and managed by RegionServers in the form of regions.
    - **Low Latency for Online Business Use**: Handles large amounts of enterprise data efficiently and provides high-speed data access.

### Using HBase and Hive Together

Note: Integration between HBase and Hive is incompatible in the some versions. Therefore, need to recompile `hive-hbase-handler`.

**Environment Setup**:
Since Hive operations might affect HBase, Hive needs the necessary HBase JAR files. We can copy or create soft links for these JAR files.

```bash
export HBASE_HOME=/opt/module/hbase
export HIVE_HOME=/opt/module/hive
ln -s $HBASE_HOME/lib/hbase-common-1.3.1.jar $HIVE_HOME/lib/hbase-common-1.3.1.jar
ln -s $HBASE_HOME/lib/hbase-server-1.3.1.jar $HIVE_HOME/lib/hbase-server-1.3.1.jar
ln -s $HBASE_HOME/lib/hbase-client-1.3.1.jar $HIVE_HOME/lib/hbase-client-1.3.1.jar
ln -s $HBASE_HOME/lib/hbase-protocol-1.3.1.jar $HIVE_HOME/lib/hbase-protocol-1.3.1.jar
ln -s $HBASE_HOME/lib/hbase-it-1.3.1.jar $HIVE_HOME/lib/hbase-it-1.3.1.jar
ln -s $HBASE_HOME/lib/htrace-core-3.1.0-incubating.jar $HIVE_HOME/lib/htrace-core-3.1.0-incubating.jar
ln -s $HBASE_HOME/lib/hbase-hadoop2-compat-1.3.1.jar $HIVE_HOME/lib/hbase-hadoop2-compat-1.3.1.jar
ln -s $HBASE_HOME/lib/hbase-hadoop-compat-1.3.1.jar $HIVE_HOME/lib/hbase-hadoop-compat-1.3.1.jar
```

Modify the ZooKeeper properties in `hive-site.xml`:

```xml
<property>
    <name>hive.zookeeper.quorum</name>
    <value>hadoop102,hadoop103,hadoop104</value>
    <description>The list of ZooKeeper servers to talk to. This is only needed for read/write locks.</description>
</property>
<property>
    <name>hive.zookeeper.client.port</name>
    <value>2181</value>
    <description>The port of ZooKeeper servers to talk to. This is only needed for read/write locks.</description>
</property>
```

**Example 1**: Create a Hive table, associate it with an HBase table, and insert data into the Hive table, which will affect the HBase table.

Steps to Implement:
1. **Create a table in Hive and associate it with HBase**
    ```sql
    CREATE TABLE hive_hbase_emp_table (
        empno int,
        ename string,
        job string,
        mgr int,
        hiredate string,
        sal double,
        comm double,
        deptno int
    )
    STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
    WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,info:ename,info:job,info:mgr,info:hiredate,info:sal,info:comm,info:deptno")
    TBLPROPERTIES ("hbase.table.name" = "hbase_emp_table");
    ```
    Tip: After creating the table, check both Hive and HBase to see the corresponding tables created.

2. **Create a temporary intermediate table in Hive for loading data**
    ```sql
    CREATE TABLE emp (
        empno int,
        ename string,
        job string,
        mgr int,
        hiredate string,
        sal double,
        comm double,
        deptno int
    )
    ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t';
    ```

3. **Load data into the intermediate Hive table**
    ```bash
    hive> LOAD DATA LOCAL INPATH '/home/admin/softwares/data/emp.txt' INTO TABLE emp;
    ```

4. **Insert data from the intermediate table into the Hive table associated with HBase**
    ```bash
    hive> INSERT INTO TABLE hive_hbase_emp_table SELECT * FROM emp;
    ```

5. **Check if the data has been successfully inserted into both Hive and the associated HBase table**
    - Hive:
      ```sql
      hive> SELECT * FROM hive_hbase_emp_table;
      ```
    - HBase:
      ```bash
      Hbase> SCAN 'hbase_emp_table'
      ```

**Example 2**: Assume the `hbase_emp_table` already exists in HBase, and create an external table in Hive to associate with the `hbase_emp_table` for data analysis.

Note: Complete Example 1 before starting Example 2.

Steps to Implement:
1. **Create an external table in Hive**
    ```sql
    CREATE EXTERNAL TABLE relevance_hbase_emp (
        empno int,
        ename string,
        job string,
        mgr int,
        hiredate string,
        sal double,
        comm double,
        deptno int
    )
    STORED BY 'org.apache.hadoop.hive.hbase.HBaseStorageHandler'
    WITH SERDEPROPERTIES ("hbase.columns.mapping" = ":key,info:ename,info:job,info:mgr,info:hiredate,info:sal,info:comm,info:deptno")
    TBLPROPERTIES ("hbase.table.name" = "hbase_emp_table");
    ```

2. **Use Hive functions to perform some analysis operations on the associated HBase table**
    ```sql
    hive (default)> SELECT * FROM relevance_hbase_emp;
    ```