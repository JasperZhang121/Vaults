```assembly
;----------------------------------------------------------------------------
DA_DR        EQU 90h   ; Existing read-only data segment type value
DA_DRW       EQU 92h   ; Existing read-write data segment attribute value
DA_DRWA      EQU 93h   ; Existing accessed read-write data segment type value
DA_C         EQU 98h   ; Existing execute-only code segment attribute value
DA_CR        EQU 9Ah   ; Existing execute-read code segment attribute value
DA_CCO       EQU 9Ch   ; Existing execute-only conforming code segment attribute value
DA_CCOR      EQU 9Eh   ; Existing execute-read conforming code segment attribute value
;----------------------------------------------------------------------------
DA_32        EQU 4000h ; 32-bit segment

DA_DPL0      EQU 00h   ; DPL = 0
DA_DPL1      EQU 20h   ; DPL = 1
DA_DPL2      EQU 40h   ; DPL = 2
DA_DPL3      EQU 60h   ; DPL = 3
;----------------------------------------------------------------------------
%macro Descriptor 3
    dw %2 & 0FFFFh               ; Segment limit 1             (2 bytes)
    dw %1 & 0FFFFh               ; Segment base 1              (2 bytes)
    db (%1 >> 16) & 0FFh         ; Segment base 2              (1 byte)
    dw ((%2 >> 8) & 0F00h) | (%3 & 0F0FFh) ; Attribute 1 + Segment limit 2 + Attribute 2    (2 bytes)
    db (%1 >> 24) & 0FFh         ; Segment base 3              (1 byte)
%endmacro ;

org 07c00h
    jmp LABEL_BEGIN

[SECTION .gdt]
; GDT
;                                         Segment base,     Segment limit    , Attributes
LABEL_GDT:          Descriptor       0,                0, 0             ; Null descriptor
LABEL_DESC_CODE32:  Descriptor       0, SegCode32Len - 1, DA_C + DA_32    ; Non-conforming code segment, 32-bit
LABEL_DESC_VIDEO:   Descriptor   0B8000h,           0ffffh, DA_DRW         ; Video memory base address
; End of GDT

GdtLen      equ $ - LABEL_GDT ; Length of GDT
GdtPtr      dw GdtLen - 1   ; GDT limit
        dd 0         ; GDT base address

; GDT selectors
SelectorCode32      equ LABEL_DESC_CODE32 - LABEL_GDT
SelectorVideo       equ LABEL_DESC_VIDEO - LABEL_GDT
; End of [SECTION .gdt]

[SECTION .s16]
[BITS 16]
LABEL_BEGIN:
    mov ax, cs
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov sp, 0100h

    ; Initialize the 32-bit code segment descriptor
    xor eax, eax
    mov ax, cs
    shl eax, 4
    add eax, LABEL_SEG_CODE32
    mov word [LABEL_DESC_CODE32 + 2], ax
    shr eax, 16
    mov byte [LABEL_DESC_CODE32 + 4], al
    mov byte [LABEL_DESC_CODE32 + 7], ah

    ; Prepare for loading GDTR
    xor eax, eax
    mov ax, ds
    shl eax, 4
    add eax, LABEL_GDT   ; eax <- GDT base address
    mov dword [GdtPtr + 2], eax   ; [GdtPtr + 2] <- GDT base address

    ; Load GDTR
    lgdt [GdtPtr]

    ; Disable interrupts
    cli

    ; Enable A20 line
    in al, 92h
    or al, 00000010b
    out 92h, al

    ; Prepare for switching to protected mode
    mov eax, cr0
    or eax, 1
    mov cr0, eax

    ; Actually enter protected mode
    jmp dword SelectorCode32:0   ; Executing this instruction will load SelectorCode32 into cs and jump to Code32Selector:0
; End of [SECTION .s16]


[SECTION .s32] ; 32-bit code segment. Entered from real mode.
[BITS 32]

LABEL_SEG_CODE32:
    mov ax, SelectorVideo
    mov gs, ax        ; Video segment selector (destination)

    mov edi, (80 * 3 + 0) * 2 ; Screen: 3rd row, 0th column
    mov ah, 0Ch       ; 0000: Black background, 1100: Red text
    mov al, 'D'
    mov [gs:edi], ax

    ; Halt here
    jmp $

SegCode32Len equ $ - LABEL_SEG_CODE32
; End of [SECTION .s32]
times 361 db 0
dw 0xaa55
```

check:[[2. Addressing Mode]]